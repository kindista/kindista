(in-package :kindista)

(defun strcat (&rest items)
  (format nil "窿轸屙螬ㄤ彐磲泸螳é蝈篝篝蜷铉螬啜泔钽狒孱狒篝蜷铉荔趄轭珞┅ㄤ彐磲泸鏖翳骈戾祜汶è疳翳脲轭翦蝣犰怙澌怙澌⑶弭犷屮沆躞轹祜汶镱骈戾涉祜汶汜铑雉忮镡翎轭邃脲屦趄轭徭翦麽轸轭麒殪澧戾è祜汶疳翳ㄧ孱簌愆祜汶骈戾ㄧ孱簌愆┅啜戾è祜汶疳翳ㄦ矧磲铋岙祜汶钺礤篝蜷铉疳翳┅┅躅鏖钿痱雉邈痱镧祜镳烘矧祜汶骈戾镳孱祜汶疳翳轰轵邈糸镱猴豸瘐洪姝屮轶趔铋洪姝滹弩铒舡屮轶恒蝈狒濠乎铘殪祜汶骈戾轰箪邋矧轭翦蝣犰爱暴烘轭犰禊ㄣ祜箦祜汶骈戾┅棱镤ㄩ珙矧瀛弪蝻蝮ㄤ屐弭瀛骈戾祜汶疳翳┅┅┅ㄤ彐磲泸梏盱é怙澌怙澌戾è簌ㄧ孱簌愆┅啜鏖翳梏盱秕麴豸麸篝蜷铉ì簌愆棱镤┅ㄤ彐磲泸狍弭痨徙鲠祯濠啜犷狃栾蜥汉簌礅镬殂箦翩痨徙鲠祯濠ㄤ彐躅篚忪轶扉篝镳糸镱犰篝狎戾铉翳麒孱篝狎ㄤ雉轫弩ㄩ篝狎舂箦翩扉篝ㄣ潋扉篝┅┅ㄩ戾铉翳麒孱扉篝ㄩ翦ㄦ矧骝镯麸戾铉翳ㄣ镬戾泗ㄣ狎扉篝┅ㄩㄣ潋扉篝箦翩扉篝ㄣ潋扉篝┅ㄦ轭轶瑭┅ㄣ镳扉篝扉篝┅ㄤ彐躅轭翦蝮邈糸镱骘躜翳扉篝扉篝博ㄩ铘弪箦泗轱扉篝扉篝弘妁＇骘躜翳┅ㄤ彐躅篝蜷铉轭翦蝮邈糸镱扉篝扉篝博ㄩ铘弪箦泗轱扉篝扉篝呼弩＇篝蜷铉僵ㄤ彐躅徙糸鲩豉蜥铍ㄩ翦愆轶轸骝镯骝殄钿栾磲铢疱镳戾扉脲轸栾蝈沐铘栾沆矬轶轸戾è骝殄钿ㄧ弭躞弪烘镬祜鏖铉┅ㄡ珏ōㄧ弭躅轹弪筢飙糸礤矧蝈篚祠泸遽翦轸屙癌┅ㄤ轶翎钽ㄡ轵溟篝犷沐灬糸趱溴祜铉轸蹁濯蝈篚祠灬糸趱溴轸屙蝈篚祠祜铉轸蹁轸屙┅┅蝻躅ō徵ǒ辈鞍鞍祜ǐ溟篝犷沐穿┅ㄩㄩ铘弪箦泗轱骝殄钿蝈篚祠疱镳戾轸屙┅付窗癌í戾铉翳祜鲥蝈篚祠殇轸屙┅卑鞍鞍┅┅ㄤ彐躅轭鲥铘矧蜥铍ㄩ翦愆戾è徵ōㄧ弭躅轹弪筢飙糸礤矧蝈篚祠泸遽翦轸屙癌┅祜鲥磲戾铉翳祜鲥蝈篚祠殇轸屙┅┅┅íǒ蛋祜ǐǒ徵付窗癌订┅ㄥ痿祜鲥爱畅┅ㄤ彐躅轭扉铄糸礤篝犴糸礤脲豉疱躜飑戾è轭铄ㄨ繇麒孱豉疱ㄨ繇篝豉疱┅篝ㄨ蹴犷辁瀛躅轹弪筢飙糸礤糸礤┅┅ㄨ繇ê箴犷恒灬篌Ⅳ轫弩翎眇轰狒岘糸礤糸礤轰狒岘豉疱豉疱ㄩ躜ㄨ繇ê鸿蝈躜篝轭铄颟┅篝轭铄颟┅┅ㄤ彐躅躜飙泔眇矬ㄢ狍蝈篝疳蜥眢ㄤè疳蜥憝篝蜷铉ī┅è铒疳蜥眢ㄦ矧磲铋狺狺狺蕈" base (if param-strings "?" "") param-strings))
      (when (cadr params)
        (push (if (consp (cadr params))
                (format nil "a=狺瞢" (car params) (cadr params))
                (format nil "a=a" (car params) (cadr params)))
              param-strings))
      (setf params (cddr params))))

(defun distance-string (miles)
  (let ((distance (/ (round miles 0.5) 2)))
    (cond
      ((<= distance 1/2)
       "1/2 mile")
      ((eql distance 1)
       "1 mile")
      ((typep distance 'ratio)
       (format nil "1$ miles" (coerce distance 'float)))    
      (t
       (format nil "d miles" distance)))))

(defvar *state-options*
  (html
    (dolist (state '("AL" "AK" "AZ" "AR" "CA" "CO" "CT" "DE" "DC" "FL" "GA" "HI" "ID" "IL" "IN" "IA" "KS" "KY" "LA" "ME" "MD" "MA" "MI" "MN" "MS" "MO" "MT" "NE" "NV" "NH" "NJ" "NM" "NY" "NC" "ND" "OH" "OK" "OR" "PA" "RI" "SC" "SD" "TN" "TX" "UT" "VT" "VA" "WA" "WV" "WI" "WY"))
      (htm
        (:option :value state (str state))))))

(defun state-options (&optional selected)
  (html
    (dolist (state '("AL" "AK" "AZ" "AR" "CA" "CO" "CT" "DE" "DC" "FL" "GA" "HI" "ID" "IL" "IN" "IA" "KS" "KY" "LA" "ME" "MD" "MA" "MI" "MN" "MS" "MO" "MT" "NE" "NV" "NH" "NJ" "NM" "NY" "NC" "ND" "OH" "OK" "OR" "PA" "RI" "SC" "SD" "TN" "TX" "UT" "VT" "VA" "WA" "WV" "WI" "WY"))
      (htm
        (:option :value state :selected (if (equalp selected state) "selected" nil) (str state))))))

(defun empty-string-p (string)
  (or (not string) (string= string "")))
