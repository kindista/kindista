;;; Copyright 2012-2013 CommonGoods Network, Inc.
;;;
;;; This file is part of Kindista.
;;;
;;; Kindista is free software: you can redistribute it and/or modify it
;;; under the terms of the GNU Affero General Public License as published
;;; by the Free Software Foundation, either version 3 of the License, or
;;; (at your option) any later version.
;;;
;;; Kindista is distributed in the hope that it will be useful, but WITHOUT
;;; ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
;;; FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Affero General Public
;;; License for more details.
;;;
;;; You should have received a copy of the GNU Affero General Public License
;;; along with Kindista.  If not, see <http://www.gnu.org/licenses/>.

(in-package :kindista)

(defparameter +number-scanner+ (create-scanner "^\\d+$"))
(defparameter +full-name-scanner+ (create-scanner "^([a-zA-Z]+\\.? )[a-zA-Z]+"))
(defparameter +email-scanner+ (create-scanner
                                 "^[a-zA-Z0-9._%+-]+@[a-zA-Z0-9.-]+\\.[a-zA-Z]{2,6}$"))
(defparameter *english-list*
  "［箕狺箕犷狺夯利狺［滑犷夯蔺蔺ㄤ彐疳蜥礤翦轲筱犷铄颢ㄣ蝈狒瀛筱犷铄⑥ㄜ茕谍┄ō┄苘潲待┅郡┅ㄤ彐疳蜥礤翦痂镱瀛筱犷铄颢ㄣ蝈狒瀛筱犷铄亢亢苘勘圮塄苘筝亢郛蒈荏┛┛亢ㄜ荏ㄛ箔馆臂安馆鄄馆郯箔篙秉鄄馆郯箔篙郯箔馆┷苘┸荏莳ㄛ箔馆臂安馆鄄馆郯箔篙秉鄄馆郯箔篙郯箔馆┅苘螵亢郛蒈荏┛┄鄄馆臂安馆鄄馆郯箔馆秉鄄馆郯箔馆昌┸荏亢郛蒈荏┛ㄛ碍馆待┅ㄤ彐疳蜥礤翦溽翦筱犷铄颢ㄣ蝈狒瀛筱犷铄⑥ò郾馆臂碍草郾馆┄郫荸ò郾馆驰氨蔹郾草郯馆郯馆┄郫荸ū观碍馆郯馆鄄馆郯馆郯馆郯馆─┅ㄤ彐疳蜥礤翦糸礤筱犷铄颢ㄣ蝈狒瀛筱犷铄⑥ò郾馆臂碍草郾馆┄痕碍递郯馆┛卡狳俩瘘些苘卡睃桐苘郡┅ㄤ彐躅篝蜚狒é蝈篝轸屙螬ㄦ矧磲铋窿轸屙螬ㄤ彐磲泸螳é蝈篝篝蜷铉螬啜泔钽狒孱狒篝蜷铉荔趄轭珞┅ㄤ彐躅鲠扉溽翦钺礤篝蜷铉筱犷骢祆钺礤筱犷铄颢篝蜷铉┅ㄤ彐躅鲠扉溽翦屙衢篝蜷铉筱犷屙衢飙筱犷铄颢篝蜷铉┅ㄤ彐躅骟钽篝蝈犴ㄦ轭轶璀秕麴豸篝蝈犴筲痫箝烘簌钽筲痫箝烘殪瀛溴筱蜷痿矧篝蝈犴┅ㄤ彐磲泸鏖翳骈戾祜汶è疳翳脲轭翦蝣犰怙澌怙澌⑶弭犷屮沆躞轹祜汶镱骈戾涉祜汶汜铑雉忮镡翎轭邃脲屦趄轭徭翦麽轸轭麒殪澧戾è祜汶疳翳ㄧ孱簌愆祜汶骈戾ㄧ孱簌愆┅啜戾è祜汶疳翳ㄦ矧磲铋岙祜汶钺礤篝蜷铉疳翳┅┅躅鏖钿痱雉邈痱镧祜镳烘矧祜汶骈戾镳孱祜汶疳翳轰轵邈糸镱猴豸瘐洪姝屮轶趔铋洪姝滹弩铒舡屮轶恒蝈狒濠乎铘殪祜汶骈戾轰箪邋矧轭翦蝣犰爱暴烘轭犰禊ㄣ祜箦祜汶骈戾┅棱镤ㄩ珙矧瀛弪蝻蝮ㄤ屐弭瀛骈戾祜汶疳翳┅┅┅ㄤ彐磲泸梏盱é怙澌怙澌戾è簌ㄧ孱簌愆┅啜鏖翳梏盱秕麴豸麸篝蜷铉ì簌愆棱镤┅ㄤ彐磲泸狍弭痨徙鲠祯濠啜犷狃栾蜥汉簌礅镬殂箦翩痨徙鲠祯濠ㄤ彐躅篚忪轶扉篝镳糸镱犰篝狎泔躅舂麒孱篝狎箦翩扉篝铘桡潋篝狎扉篝┅戾è戾铉翳戾铉翳扉篝┅ㄣ镱è矧铒泔躅舂窘泔躅戾铉翳┅鲠祯弩扉篝铋飑鲠祯弩篚怏羼扉篝泔躅舂舂┅┅ㄤ彐躅轭翦蝮邈糸镱骘躜翳扉篝扉篝博ㄩ铘弪箦泗轱扉篝扉篝弘妁＇骘躜翳┅ㄤ彐躅篝蜷铉轭翦蝮邈糸镱扉篝扉篝博ㄩ铘弪箦泗轱扉篝扉篝呼弩＇篝蜷铉僵ㄤ彐躅屙衢祗骝镯篝蜷铉篝蜷铉ㄩ翦ㄦ矧屙衢轭箴扉痧泸搴蝈珏蝈痨徙瀛犰军饥篝蜷铉滹黝汜箦篝蜷铉┅麒孱痧泸搴筱犷屙衢飙筱犷铄颢屙衢飑ㄣ镬戾泗屙衢飑┅ㄤ彐躅徙糸鲩豉蜥铍ㄩ翦愆戾è泔铘徙趔ㄧ弭躞弪烘镬祜鏖铉┅ㄡ珏ōㄧ弭躅轹弪筢飙糸礤矧蝈篚祠糸礤轸屙癌┅ㄤ轶翎钽ㄡ轵溟篝犷沐灬糸趱溴祜铉轸蹁濯蝈篚祠灬糸趱溴轸屙蝈篚祠祜铉轸蹁轸屙┅┅蝻躅ō徵ǒ辈鞍鞍祜ǐㄩㄩ铘弪箦泗轱泔铘徙趔蝈篚祠疱镳戾轸屙┅溟篝犷沐穿┅í戾铉翳祜鲥蝈篚祠殇轸屙┅蛋鞍癌┅┅ㄤ彐躅篝犰瀛弼孱麴ㄩ翦愆戾è篝犰孱弩ō矧蝈篚祠糸礤轸屙癌ōㄧ弭躅轹弪筢飙糸礤溽轭箦泔钿螳┅┅麒孱篝犰孱弩癌舂┅ㄤ彐躅弼孱舡蜥铍ㄩ翦愆戾è泔铘徙趔ㄧ弭躞弪烘镬祜鏖铉┅ㄣ躜蝈铘铄篌ㄡ怏ō矧蝈篚祠糸礤轸屙癌ㄧ弭躅轹弪筢飙糸礤┅┅ㄤ轶翎钽ㄡ轵溟篝犷沐灬糸趱溴祜铉轸蹁濯蝈篚祠灬糸趱溴轸屙蝈篚祠祜铉轸蹁轸屙┅┅蝻躅ō沲蝌孱纛弩ǒ辈鞍鞍祜ǐㄩㄩ铘弪箦泗轱泔铘徙趔蝈篚祠疱镳戾轸屙┅溟篝犷沐穿┅í戾铉翳祜鲥蝈篚祠殇轸屙┅蛋鞍癌┅┅ㄤ彐躅轭鲥铘矧蜥铍ㄩ翦愆戾è徵ōㄧ弭躅轹弪筢飙糸礤矧蝈篚祠糸礤轸屙癌┅祜鲥磲戾铉翳祜鲥蝈篚祠殇轸屙┅┅┅íǒ蛋祜ǐǒ徵付窗癌订┅ㄥ痿祜鲥爱畅┅ㄤ彐躅蝈骝弩璀轸屙糸礤轭轭溴弩ㄩ脲糸礤ㄧ弭躅轹弪筢飙糸礤┅戾舄è蝈篚祠ㄧ弭栳箬殇溻蝈篚祠螵┅豉疱蝈篚祠豉疱蝈篚祠┅ㄩ翦ㄤ殇┅ㄢㄣ狍豉疱è矧猴骀弪候羼蹂篝ㄧ弭轸屙衡┅ê珧狒轸蹁ㄧ弭轸屙横豸栾颟┅┅麒孱ㄡ钿ㄥ耢躞弪殇怡矧ㄥ豉疱虹蜥糸趱溴ㄥ豉疱猴骀弪ㄥ豉疱候羼蹂篝┅箦翩蝈篚祠糸礤蝈篚祠糸礤鏖翳祜汶邃栳箬翎忪í徙糸鲩豉疱蝮镱轭溴ㄡ箦翩ㄧ弭栳箬怡徙糸鲩豉疱蝮镱轭溴箫螋轸＇弘妁＇蝈篚祠糸礤┅躅戾篌蝈篚祠糸礤蝈篚祠ōㄧ弭躅轹弪筢飙糸礤钡档舶鞍┅躅戾篌蝈篚祠糸礤蝈篚祠ōㄧ弭躅轹弪筢飙糸礤驳共鞍癌鏖翳眭翦í蝈沐铘徙糸鲩豉眭翦箦翩蝈沐铘徙糸鲩豉轭溴箫螋瘐箬铄蝈篚祠蝈沐铘徙糸鲩豉轭溴＇弘妁＇蝈篚祠糸礤┅┅ㄧ屣轭溴轭箦螋徙糸鲩豉珏锃轭溴蝈篚祠┅┅ㄤ彐躅躜飙泔眇矬ㄢ狍蝈篝疳蜥眢ㄤè疳蜥憝篝蜷铉ī┅è铒疳蜥眢ㄦ矧磲铋狺狺狺蕈" base (if param-strings "?" "") param-strings))
      (when (cadr params)
        (push (if (consp (cadr params))
                (format nil "a=狺瞢" (car params) (cadr params))
                (format nil "a=a" (car params) (cadr params)))
              param-strings))
      (setf params (cddr params))))

(defun ellipsis (text &optional (length 160) &key see-more)
  (let ((newtext (subseq text 0 (min (length text) length))))
    (if (> (length text) length)
      (html
        (str newtext)
        "..." 
        (when see-more
          (htm (:a :href see-more " see more"))))
      newtext)))

(defun html-text (string)
  (if string
    (regex-replace-all "\\n" (escape-for-html string) "<br>")
    ""))

(defun distance-string (miles)
  (let ((distance (/ (round miles 0.5) 2)))
    (cond
      ((<= distance 1/2)
       "1/2 mile")
      ((eql distance 1)
       "1 mile")
      ((typep distance 'ratio)
       (format nil "1$ miles" (coerce distance 'float)))    
      (t
       (format nil "d miles" distance)))))

(defvar *state-options*
  (html
    (dolist (state '("AL" "AK" "AZ" "AR" "CA" "CO" "CT" "DE" "DC" "FL" "GA" "HI" "ID" "IL" "IN" "IA" "KS" "KY" "LA" "ME" "MD" "MA" "MI" "MN" "MS" "MO" "MT" "NE" "NV" "NH" "NJ" "NM" "NY" "NC" "ND" "OH" "OK" "OR" "PA" "RI" "SC" "SD" "TN" "TX" "UT" "VT" "VA" "WA" "WV" "WI" "WY"))
      (htm
        (:option :value state (str state))))))

(defun state-options (&optional selected)
  (html
    (dolist (state '("AL" "AK" "AZ" "AR" "CA" "CO" "CT" "DE" "DC" "FL" "GA" "HI" "ID" "IL" "IN" "IA" "KS" "KY" "LA" "ME" "MD" "MA" "MI" "MN" "MS" "MO" "MT" "NE" "NV" "NH" "NJ" "NM" "NY" "NC" "ND" "OH" "OK" "OR" "PA" "RI" "SC" "SD" "TN" "TX" "UT" "VT" "VA" "WA" "WV" "WI" "WY"))
      (htm
        (:option :value state :selected (if (equalp selected state) "selected" nil) (str state))))))

(defun empty-string-p (string)
  (or (not string) (string= string "")))

(defun alpha-people-links (userid-list)
  (mapcar
    #'cdr
    (sort
      (iter (for id in (copy-list userid-list))
            (let* ((name (db id :name))
                   (link (html (:a :href (strcat "/people/" (username-or-id id))
                                         (str name)))))
              (collect (cons name link))))
     #'string-lessp :key #'car)))

(defun person-link (id)
  (html
    (:a :href (s+ "/people/" (username-or-id id)) (str (getf (db id) :name)))))

(defun name-list (ids &key (func #'person-link) (minimum-links 3))
  (let ((links (mapcar func (subseq ids 0 (min minimum-links (length ids))))))
    (format nil *english-list* (cond
                                ((> (length ids) (+ minimum-links 1))
                                 (append links (list (strcat "and " (- (length ids) minimum-links) " others"))))
                                ((eql (length ids) (+ minimum-links 1))
                                 (append links (list (funcall func (nth 3 ids)))))
                                (t links)))))

(defun name-list-all (ids)
  (format nil *english-list* (mapcar #'person-link ids)))

(defun pending-flash (action)
  (flash (s+ "Your account hasn't been fully activated yet. "
             "If you haven't already done so, please post some offers. "
             "After we review your initial activity you will be able to "
             action ".")
         :error t))

(defun pending-disclaimer (&optional type)
  (when (getf *user* :pending)
    (html
      (:p :class "err"
        (:em (:strong "Please note: ")
          "This "
          (str (aif type it "item"))
          " will be displayed on Kindista after we have a chance to review "
          "your account and confirm that you're not a spammer. "
          (:strong "You won't be able to send messages to other Kindista members "
          "until you post some offers and we have a chance to review your "
          "initial activity.")))
      (:br))))
